plugins {
  id "architectury-plugin" version "3.4-SNAPSHOT"
  id "dev.architectury.loom" version "1.4-SNAPSHOT"
  id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group


sourceSets {
  testmod {
    compileClasspath += main.compileClasspath
    runtimeClasspath += main.runtimeClasspath
  }
}

loom {
  accessWidenerPath = file("src/main/resources/fantasy.accesswidener")

  forge {
    convertAccessWideners = true
    extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

    mixinConfigs = [
      "fantasy.mixins.json"
    ]
  }

  runs {
    testmodClient {
      client()
      name = "Testmod Client"
      source sourceSets.testmod
      mods {
        testmod {
          source sourceSets.testmod
        }
        main {
          source sourceSets.main
        }
      }
    }
    testmodServer {
      server()
      name = "Testmod Server"
      source sourceSets.testmod
      mods {
        testmod {
          source sourceSets.testmod
        }
        main {
          source sourceSets.main
        }
      }
    }
  }
}

architectury {
  minecraft = rootProject.minecraft_version
  platformSetupLoomIde()
  forge()
}

repositories {
  maven { url = "https://maven.gegy.dev/" }
  maven { url = "https://maven.nucleoid.xyz/" }
  maven { url "https://maven.su5ed.dev/releases" }
}


loom {
  silentMojangMappingsLicense()
}

dependencies {
  minecraft "com.mojang:minecraft:${project.minecraft_version}"
  mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
  forge "net.minecraftforge:forge:${forge_version}"

  modApi("dev.su5ed.sinytra.fabric-api:fabric-api:0.91.0+1.10.8+1.20.1")

  modLocalRuntime "eu.pb4:polymer-reg-sync-manipulator:0.5.5+1.20.1"

  testmodImplementation sourceSets.main.output
}

loom {
  silentMojangMappingsLicense()
}

processResources {
  inputs.property "version", project.version

  filesMatching("META-INF/mods.toml") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile).configureEach {
  it.options.encoding = "UTF-8"
  it.options.release = 17
}

java {
  withSourcesJar()
}

jar {
  from "LICENSE"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact(remapJar) {
        builtBy remapJar
      }
      artifact(sourcesJar) {
        builtBy remapSourcesJar
      }
    }
  }

  repositories {
    def env = System.getenv()
    if (env.MAVEN_URL) {
      maven {
        credentials {
          username env.MAVEN_USERNAME
          password env.MAVEN_PASSWORD
        }
        url env.MAVEN_URL
      }
    } else {
      mavenLocal()
    }
  }
}
